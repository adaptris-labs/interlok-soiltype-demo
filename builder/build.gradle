plugins {
  id "distribution"
}

ext {
  interlokCoreVersion = '3.9.0-RELEASE'
  interlokWarVersion = interlokCoreVersion
  interlokInstallDirectory = project.hasProperty('interlokInstallDirectory') ? project.getProperty('interlokInstallDirectory') :  "/opt/interlok"
  log4j2Version = '2.12.1'
  slf4jVersion = '1.7.28'
  swaggerVersion = "2.2.3"
  swaggerDistribution = "https://github.com/swagger-api/swagger-ui/archive/v${swaggerVersion}.zip"
  swaggerRewriteUrl = "http://localhost:8080/lookups/swagger"
  lineSeparator = System.getProperty("line.separator")
}

distTar.enabled=false
distZip.enabled=true

repositories {
  mavenCentral()
  maven { url "https://nexus.adaptris.net/nexus/content/groups/public" }
  maven { url "https://nexus.adaptris.net/nexus/content/groups/interlok" }
}


configurations() {
  interlokRuntime{}
  interlokJavaDocs{}
  interlokWar{}
  all*.exclude group: 'c3p0'
  all*.exclude group: 'commons-logging'
  all*.exclude group: 'javamail'
  all*.exclude group: 'javax.mail', module: 'mail'
  all*.exclude group: 'org.glassfish.hk2.external'
  all*.exclude group: 'xalan', module: 'xalan'
  all*.exclude group: 'net.sf.saxon', module: 'saxon'
  all*.exclude group: 'org.codehaus.woodstox'
  all*.exclude group: 'org.eclipse.jetty.orbit', module: 'javax.mail.glassfish'
  all*.exclude group: 'com.vaadin.external.google', module: 'android-json'
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, "seconds"
}

dependencies {
  interlokRuntime  group: "com.adaptris", name: "interlok-core-apt", version: "$interlokCoreVersion", changing: true
  interlokRuntime  group: "com.adaptris", name: "interlok-boot", version: "$interlokCoreVersion", changing: true
  interlokRuntime  group: "com.adaptris", name: "interlok-common", version: "$interlokCoreVersion", changing: true
  interlokRuntime  group: "com.adaptris", name: "interlok-logging", version: "$interlokCoreVersion", changing: true
  interlokRuntime  group: "com.adaptris", name: "interlok-core", version: "$interlokCoreVersion", changing: true
  interlokRuntime  group: "com.adaptris", name: "interlok-json", version: "$interlokCoreVersion", changing: true
  interlokRuntime  group: "com.adaptris", name: "interlok-apache-http", version: "$interlokCoreVersion", changing: true
  interlokRuntime  group: "com.adaptris", name: "interlok-licensing", version: "$interlokCoreVersion", changing: true
  interlokRuntime  group: "com.adaptris", name: "interlok-ehcache", version: "$interlokCoreVersion", changing: true
  interlokRuntime  group: "com.adaptris", name: "interlok-xinclude", version: "$interlokCoreVersion", changing: true
  interlokRuntime  group: "com.adaptris", name: "interlok-varsub", version: "$interlokCoreVersion", changing: true

  // If you want javadocs.
  interlokJavaDocs group: "com.adaptris", name: "interlok-core", version: "$interlokCoreVersion", changing: true, classifier: "javadoc", transitive: false
  interlokJavaDocs group: "com.adaptris", name: "interlok-apache-http", version: "$interlokCoreVersion", changing: true, classifier: "javadoc", transitive: false
  interlokJavaDocs group: "com.adaptris", name: "interlok-json", version: "$interlokCoreVersion", changing: true, classifier: "javadoc", transitive: false
  interlokJavaDocs group: "com.adaptris", name: "interlok-ehcache", version: "$interlokCoreVersion", changing: true, classifier: "javadoc", transitive: false

  interlokRuntime group: "mysql", name: "mysql-connector-java", version: "5.1.47"
  interlokRuntime group: "org.slf4j", name: "slf4j-api", version: "$slf4jVersion"
  interlokRuntime group: "org.slf4j", name: "jcl-over-slf4j", version: "$slf4jVersion"
  interlokRuntime group: "org.slf4j", name: "jul-to-slf4j", version: "$slf4jVersion"

  interlokRuntime group: "org.apache.logging.log4j", name: "log4j-core", version: "$log4j2Version"
  interlokRuntime group: "org.apache.logging.log4j", name: "log4j-1.2-api", version: "$log4j2Version"
  interlokRuntime group: "org.apache.logging.log4j", name: "log4j-slf4j-impl", version: "$log4j2Version"
  interlokRuntime group: "org.apache.logging.log4j", name: "log4j-api", version: "$log4j2Version"

  interlokWar group: "com.adaptris.ui", name: "interlok", version: "$interlokWarVersion", ext: "war", changing: true
}


task swagger () {
  outputs.file new File("${buildDir}/swagger/swagger-ui-${swaggerVersion}/dist")
  doLast {
    mkdir("${buildDir}")
    ant.get(src: "${swaggerDistribution}", dest: "${buildDir}/swagger-${swaggerVersion}.zip", usetimestamp: 'true', quiet: 'true')
    copy {
      from zipTree("${buildDir}/swagger-${swaggerVersion}.zip")
      into "${buildDir}/swagger"
    }
    ant.replace(file: "${buildDir}/swagger/swagger-ui-${swaggerVersion}/dist/index.html", token: 'http://petstore.swagger.io/v2/swagger.json', value: "${swaggerRewriteUrl}", preserveLastModified: 'true')
    ant.replace(file: "${buildDir}/swagger/swagger-ui-${swaggerVersion}/dist/index.html", token: '        url: url,', value: "        url: url,${lineSeparator}        validatorUrl: null,", preserveLastModified: 'true')
    ant.fixcrlf(file: "${buildDir}/swagger/swagger-ui-${swaggerVersion}/dist/index.html", eol: 'lf', eof: 'remove', preserveLastModified: 'true')
    delete "${buildDir}/swagger-${swaggerVersion}.zip"
  }
}

distributions {
  main {
    contents {
      from {
        "src/main/interlok"
      }
      into('lib') {
        from(project.configurations.interlokRuntime)
      }
      into('webapps/swagger') {
        from(swagger)
      }
      into('webapps') {
        from(project.configurations.interlokWar)
      }
      into('docs/javadocs') {
        from(project.configurations.interlokJavaDocs)
      }
      rename '(.*)-[0-9]+\\..*.jar', '$1.jar'
      rename '(.*)-[0-9]+\\..*.war', '$1.war'
      duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
  }
}

installDist {
  destinationDir = new File(interlokInstallDirectory)
  preserve {
    include '**/**'
  }
}
